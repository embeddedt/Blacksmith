plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'forge'
            url = 'https://maven.neoforged.net/'
        }
    }
    group 'org.embeddedt'
    version '1.0-SNAPSHOT'
}

configurations {
    jij {
        transitive false
    }
}

project.configurations.implementation.canBeResolved = true

dependencies {
    implementation project(path: ":main", configuration: 'shadow')
    jij project(":java17")
    implementation project(":launchwrapper")
}

jar {
    manifest {
        attributes(
                "Premain-Class": "org.embeddedt.blacksmith.impl.Agent",
                "Can-Redefine-Classes": false,
                "Can-Set-Native-Method-Prefix": false
        )
    }
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

task shadowBugWorkaround(type: Jar) {
    dependsOn jar
    dependsOn tasks.getByPath(':java17:jar')
    destinationDir file('build/shadow-bug-workaround')
    baseName = 'nested-content'
    from jar
    from tasks.getByPath(':java17:jar')
}

shadowJar {
    dependsOn shadowBugWorkaround
    exclude 'META-INF/**'
    from 'extras'
    // Shadow plugin bug: explodes the nested jar.
    //from tasks.getByPath(':annotation:jar')
    // Workaround: double-nest the jar.
    from shadowBugWorkaround
}
assemble.dependsOn shadowJar

shadowJar {
    archiveClassifier.set('')
}